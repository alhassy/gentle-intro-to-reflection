# | ~C-x C-a~ | transform org ~org-agda~ blocks to literate Agda blocs        |
# | ~C-x C-o~ | transform literate Agda code delimiters to org ~org-agda~ src |
#
# Need to ensure org-indent-mode is off when going to agda.

#
#+TITLE: A Gentle Introduction to Reflection in Agda
#+DESCRIPTION: How can we use a single proof to prove two different theorems? One proof pattern, multiple invocations!
#+AUTHOR: Musa Al-hassy
#+EMAIL: alhassy@gmail.com
#+STARTUP: indent
#+PROPERTY: header-args :tangle tangled.agda :comments links

#+CATEGORIES: Agda Org Emacs
#+OPTIONS: html-postamble:nil toc:nil d:nil tag:nil
#+IMAGE: ../assets/img/org_logo.png
#+SOURCE: https://raw.githubusercontent.com/alhassy/org-agda-mode/master/literate.lagda

# INCLUDE: ~/Dropbox/MyUnicodeSymbols.org

* Abstract       :ignore:
#+BEGIN_CENTER org
*Abstract*
#+END_CENTER

/One proof for two different theorems!/

Let's learn how we can do that in Agda.

This tutorial is the result of mostly experimenting with the
[[https://agda.readthedocs.io/en/v2.5.2/language/reflection.html][documentation]] on Agda's reflection mechanism, which essentially
only exposes the reflection interface and provides a few tiny examples.
The goal of this tutorial is to contain a diverse variety of examples,
along with occasional exercises for the reader.

Examples include:
+ String manipulation of built-in identifier names. ğŸ“
+ Handy dandy combinators for AST formation: ~ğ“‹ğ“‡ğ’¶, Î»ğ“‹_â†¦_, â€¦~. ğŸ› 
+ Numerous examples of quotation of terms and types. ğŸ¯
+ Wholesale derivation of singleton types for an example datatype,
  along with derivable proofs ğŸ’› ğŸµ
+ Automating proofs that are only ~refl~ /with/ pattern matching ğŸ„
+ Discussion of C-style macros in Agda ğŸŒµ
+ Abstracting proofs patterns without syntactic overhead using macros ğŸ’ª ğŸ¼
+ Remarks on what I could not do, possibly since it cannot be done :sob:

Everything here works with Agda version 2.6.0.
#+TOC: headlines 2

* Imports

#+BEGIN_SRC org-agda
module gentle-intro-to-reflection where

open import Relation.Binary.PropositionalEquality hiding ([_])
open import Reflection hiding (_â‰Ÿ_ ; name)
open import Data.List as List
open import Relation.Nullary

open import Reflection

open import Data.Nat
open import Data.Bool
open import Data.String as String

open import Data.Unit

import Level as Level

open import Data.Char as Char
open import Relation.Unary using (Decidable)

open import Data.Product

open import Relation.Nullary
#+END_SRC

* Intro

/Reflection/ is the ability to convert program code into an abstract syntax,
a data structure that can be manipulated like any other.

Consider, for example, the tedium of writing a decidable equality for an enumerated type.
Besides being tedious and error-prone, the inexpressibility of
what should be a mechanically-derivable concept
obscures the corresponding general principle underlying it, thus foregoing
any machine assistance in ensuring any correctness or safety-ness guarantees.
Reflection allows a more economical and disciplined approach.

It is the aim of this tutorial to show how to get started with reflection in Agda.
To the best of my knowledge there is no up to date tutorial on this matter.


There are three main types in Agda's reflection mechanism:
~Name, Arg, Term~.


#+BEGIN_SRC org-agda
data RGB : Set where
  Red Green Blue : RGB
#+END_SRC
* ~NAME~ â”€Type of known identifiers                         :forward_todo_link:

~Name~ is the type of quoting identifiers, Agda names.
Elements of this type can be formed and pattern matched using
the ~quote~ keyword.

#+BEGIN_SRC org-agda
a-name : Name
a-name = quote â„•

isNat : Name â†’ Bool
isNat (quote â„•) = true
isNat _         = false

-- bad : Set â†’ Name
-- bad s = quote s  {- s is not known -}
#+END_SRC

+ ~NAME~ comes equipped with equality, ordering, and a show function.
+ Quote will not work on function arguments; the identifier must be known.

Let's show names:
#+BEGIN_SRC org-agda
_ : showName (quote _â‰¡_) â‰¡ "Agda.Builtin.Equality._â‰¡_"
_ = refl

_ : showName (quote Red) â‰¡ "gentle-intro-to-reflection.RGB.Red"
_ = refl
#+END_SRC

It would be nice to have ~Red~ be shown as just ~â€œRGB.Redâ€~.

First, let's introduce some â€˜programmingâ€™ helpers to treat Agda strings as if they
where Haskell strings, and likewise to treat predicates as decidables.
#+BEGIN_SRC org-agda
{- Like â€œ$â€ but for strings. -}
_âŸ¨ğ’®âŸ©_ : (List Char â†’ List Char) â†’ String â†’ String
f âŸ¨ğ’®âŸ© s = fromList (f (toList s))

{- This should be in the standard library; I could not locate it. -}
toDec : âˆ€ {â„“} {A : Set â„“} â†’ (p : A â†’ Bool) â†’ Decidable {â„“} {A} (Î» a â†’ p a â‰¡ true)
toDec p x with p x
toDec p x | false = no Î» ()
toDec p x | true = yes refl
#+END_SRC

We can now easily obtain the module's name, then drop it from the data constructor's name.
#+BEGIN_SRC org-agda
module-name : String
module-name = takeWhile (toDec (Î» c â†’ not (c Char.== '.'))) âŸ¨ğ’®âŸ© showName (quote Red)

_ : module-name â‰¡ "gentle-intro-to-reflection"
_ = refl

strName : Name â†’ String
strName n = drop (1 + String.length module-name) âŸ¨ğ’®âŸ© showName n
{- The â€œ1 +â€ is for the â€œ.â€ seperator in qualified names. -}

_ : strName (quote Red) â‰¡ "RGB.Red"
_ = refl
#+END_SRC

~NAME~ essentially provides us with the internal representation of a known name,
for which we can query to obtain its definition or type.
Later we will show how to get the type constructors of ~â„•~ from its name.

* ~Arg~ â”€Type of arguments

Arguments in Agda may be hidden or computationally irrelevant.
This information is captured by the ~Arg~ type.

#+BEGIN_EXAMPLE org-agda
{- Arguments can be (visible), {hidden}, or â¦ƒinstanceâ¦„ -}
data Visibility : Set where
  visible hidden instanceâ€² : Visibility

{-Arguments can be relevant or irrelevant: -}
data Relevance : Set where
  relevant irrelevant : Relevance

{- Visibility and relevance characterise the behaviour of an argument: -}
data ArgInfo : Set where
  arg-info : (v : Visibility) (r : Relevance) â†’ ArgInfo

data Arg (A : Set) : Set where
  arg : (i : ArgInfo) (x : A) â†’ Arg A
#+END_EXAMPLE

For example, let's create some helpers that make arguments of any given type ~A~:
#+BEGIN_SRC org-agda
variable {A} : Set

{- ğ“‹isible ğ“‡elevant ğ’¶rgument -}
ğ“‹ğ“‡ğ’¶ : A â†’ Arg A
ğ“‹ğ“‡ğ’¶ = arg (arg-info visible relevant)

{- ğ’½idden ğ“‡elevant ğ’¶rgument -}
ğ’½ğ“‡ğ’¶ : A â†’ Arg A
ğ’½ğ“‡ğ’¶ = arg (arg-info hidden relevant)
#+END_SRC

Below are the variable counterparts, for the ~Term~ datatype,
which will be discussed shortly.
+ Variables are De Bruijn indexed and may be applied to a list of arguments.
+ The index /n/ refers to the argument that is /n/ locations away from â€˜hereâ€™.

#+BEGIN_SRC org-agda
{- ğ“‹isible ğ“‡elevant ğ“‹ariable -}
ğ“‹ğ“‡ğ“‹ : (debruijn : â„•) (args : List (Arg Term)) â†’ Arg Term
ğ“‹ğ“‡ğ“‹ n args = arg (arg-info visible relevant) (var n args)

{- ğ’½idden ğ“‡elevant ğ“‹ariable -}
ğ’½ğ“‡ğ“‹ : (debruijn : â„•) (args : List (Arg Term)) â†’ Arg Term
ğ’½ğ“‡ğ“‹ n args = arg (arg-info hidden relevant) (var n args)
#+END_SRC

* ~Term~ â”€Type of terms

We use the ~quoteTerm~ keyword to turn a well-typed fragment of code
---concrete syntax--- into a value of the ~Term~ datatype ---the abstract syntax.
Here's the definition of ~Term~:
#+BEGIN_EXAMPLE org-agda
data Term where

  {- A variable has a De Bruijn index and may be applied to arguments. -}
  var       : (x : â„•)  (args : List (Arg Term)) â†’ Term

  {- Constructors and definitions may be applied to a list of arguments. -}
  con       : (c : Name) (args : List (Arg Term)) â†’ Term
  def       : (f : Name) (args : List (Arg Term)) â†’ Term

  {- Î»-abstractions bind one varaible; â€œtâ€ is the string name of the variable
    along with the body of the lambda.
  -}
  lam       : (v : Visibility) (t : Abs Term) â†’ Term  {- Abs A  â‰…  String Ã— A -}
  pat-lam   : (cs : List Clause) (args : List (Arg Term)) â†’ Term

  {- Telescopes, or function types; Î»-abstraction for types. -}
  pi        : (a : Arg Type) (b : Abs Type) â†’ Term

  {- â€œSet nâ€ or some term that denotes a type -}
  agda-sort : (s : Sort) â†’ Term

  {- Metavariables; introduced via quoteTerm -}
  meta      : (x : Meta) â†’ List (Arg Term) â†’ Term

  {- Literal  â‰…  â„• | Word64 | Float | Char | String | Name | Meta -}
  lit       : (l : Literal) â†’ Term

  {- Items not representable by this AST; e.g., a hole. -}
  unknown   : Term {- Treated as '_' when unquoting. -}

data Sort where
  set     : (t : Term) â†’ Sort {- A Set of a given (possibly neutral) level. -}
  lit     : (n : Nat) â†’ Sort  {- A Set of a given concrete level. -}
  unknown : Sort

data Clause where
  clause        : (ps : List (Arg Pattern)) (t : Term) â†’ Clause
  absurd-clause : (ps : List (Arg Pattern)) â†’ Clause
#+END_EXAMPLE

** Example: Simple Types

Here are three examples of â€œdefâ€ined names, the first two do not take an argument.
The last takes a visible and relevant argument, ğ“‹ğ“‡ğ’¶, that is a literal natural.
#+BEGIN_SRC org-agda
import Data.Vec as V
import Data.Fin as F

_ : quoteTerm â„• â‰¡ def (quote â„•) []
_ = refl

_ : quoteTerm V.Vec â‰¡ def (quote V.Vec) []
_ = refl

_ : quoteTerm (F.Fin 3) â‰¡ def (quote F.Fin) (ğ“‹ğ“‡ğ’¶ (lit (nat 3)) âˆ· [])
_ = refl
#+END_SRC

** Example: Simple Terms

Elementary numeric quotations:
#+BEGIN_SRC org-agda
_ : quoteTerm 1 â‰¡ lit (nat 1)
_ = refl

_ :    quoteTerm (suc zero)
     â‰¡ con (quote suc) (arg (arg-info visible relevant) (quoteTerm zero) âˆ· [])
_ = refl

{- Using our helper ğ“‹ğ“‡ğ’¶ -}
_ : quoteTerm (suc zero) â‰¡ con (quote suc) (ğ“‹ğ“‡ğ’¶ (quoteTerm zero) âˆ· [])
_ = refl
#+END_SRC

The first example below demonstrates that ~true~ is a type â€œconâ€structor
that takes no arguments, whence the ~[]~. The second example shows that
~_â‰¡_~ is a defined name, not currently applied to any arguments.
The final example has propositional equality applied to two arguments.
#+BEGIN_SRC org-agda
_ : quoteTerm true â‰¡ con (quote true) []
_ = refl

_ : quoteTerm _â‰¡_ â‰¡ def (quote _â‰¡_) []
_ = refl

_ :   quoteTerm ("b" â‰¡ "a")
    â‰¡ def (quote _â‰¡_)
      ( ğ’½ğ“‡ğ’¶ (def (quote Level.zero) [])
      âˆ· ğ’½ğ“‡ğ’¶ (def (quote String) [])
      âˆ· ğ“‹ğ“‡ğ’¶ (lit (string "b"))
      âˆ· ğ“‹ğ“‡ğ’¶ (lit (string "a")) âˆ· [])
_ = refl
#+END_SRC

Notice that a propositional equality actually has four arguments â”€a level, a type, and two argumentsâ”€
where the former two happen
to be inferrable from the latter.
Here is a more polymorphic example:
#+BEGIN_SRC org-agda
_ : âˆ€ {level : Level.Level}{Type : Set level} (x y : Type)
    â†’   quoteTerm (x â‰¡ y)
       â‰¡ def (quote _â‰¡_)
           (ğ’½ğ“‡ğ“‹ 3 [] âˆ· ğ’½ğ“‡ğ“‹ 2 [] âˆ· ğ“‹ğ“‡ğ“‹ 1 [] âˆ· ğ“‹ğ“‡ğ“‹ 0 [] âˆ· [])

_ = Î» x y â†’ refl
#+END_SRC

We will demonstrate an example of a section, say
~â‰¡_ "b"~, below when discussing lambda abstractions.

** A relationship between ~quote~ and ~quoteTerm~

Known names ~f'~ in a quoted term are denoted by a ~quote f'~ in the AST representation.
#+BEGIN_SRC org-agda
postulate A' B' : Set
postulate f' : A' â†’ B'
_ : quoteTerm f' â‰¡ def (quote f') []
_ = refl
#+END_SRC

In contrast, names that /vary/ are denoted by a ~var~ constructor in the AST representation.
#+BEGIN_SRC org-agda
module _ {A B : Set} {f : A â†’ B} where
  _ : quoteTerm f â‰¡ var 0 []
  _ = refl
#+END_SRC

** Example: Lambda Terms

First we show how reductions with lambdas works then we show how lambda functions
are represented as ~Term~ values.

~quoteTerm~ typechecks and normalises its argument before yielding a ~Term~ value.
#+BEGIN_SRC org-agda
_ : quoteTerm ((Î» x â†’ x) "nice") â‰¡ lit (string "nice")
_ = refl
#+END_SRC

Eta-reduction happens, ~f â‰ˆ Î» x â†’ f x~.
#+BEGIN_SRC org-agda
id : {A : Set} â†’ A â†’ A
id x = x

_ :   quoteTerm (Î» (x : â„•) â†’ id x)
    â‰¡ def (quote id) (ğ’½ğ“‡ğ’¶ (def (quote â„•) []) âˆ· [])
_ = refl
#+END_SRC

No delta-reduction happens; function definitions are not elaborated.
#+BEGIN_SRC org-agda
_ :   quoteTerm (id "a")
    â‰¡ def (quote id)
        (ğ’½ğ“‡ğ’¶ (def (quote String) []) âˆ·  ğ“‹ğ“‡ğ’¶ (lit (string "a")) âˆ· [])
_ = refl
#+END_SRC

Here is a simple identity function on the Booleans.
A â€œlamâ€da with a â€œvisibleâ€ â€œabsâ€tract argument named ~"x"~ is introduced
having as body merely being the 0 nearest-bound variable, applied to an empty
list of arguments.
#+BEGIN_SRC org-agda
_ : quoteTerm (Î» (x : Bool) â†’ x) â‰¡ lam visible (abs "x" (var 0 []))
_ = refl

#+END_SRC

Here is a more complicated lambda abstraction: Note that ~f a~ is represented as
the variable 0 lambdas away from the body applied to the variable 1 lambda away
from the body.
#+BEGIN_SRC org-agda
_ : quoteTerm (Î» (a : â„•) (f : â„• â†’ â„•) â†’ f a)
    â‰¡  lam visible (abs "a"
         (lam visible (abs "f"
           (var 0 (arg (arg-info visible relevant) (var 1 []) âˆ· [])))))
_ = refl
#+END_SRC

This is rather messy, let's introduce some syntactic sugar to make it more readable.
#+BEGIN_SRC org-agda
infixr 5 Î»ğ“‹_â†¦_  Î»ğ’½_â†¦_

Î»ğ“‹_â†¦_  Î»ğ’½_â†¦_ : String â†’ Term â†’ Term
Î»ğ“‹ x â†¦ body  = lam visible (abs x body)
Î»ğ’½ x â†¦ body  = lam hidden (abs x body)
#+END_SRC
Now the previous example is a bit easier on the eyes:
#+BEGIN_SRC org-agda
_ :   quoteTerm (Î» (a : â„•) (f : â„• â†’ â„•) â†’ f a)
    â‰¡ Î»ğ“‹ "a" â†¦ Î»ğ“‹ "f" â†¦ var 0 [ ğ“‹ğ“‡ğ’¶ (var 1 []) ]
_ = refl
#+END_SRC

Using that delicious sugar, let's look at the constant function a number of ways.
#+BEGIN_SRC org-agda
_ : {A B : Set} â†’   quoteTerm (Î» (a : A) (b : B) â†’ a)
                  â‰¡ Î»ğ“‹ "a" â†¦ (Î»ğ“‹ "b" â†¦ var 1 [])
_ = refl

_ :  quoteTerm (Î» {A B : Set} (a : A) (_ : B) â†’ a)
    â‰¡ (Î»ğ’½ "A" â†¦ (Î»ğ’½ "B" â†¦ (Î»ğ“‹ "a" â†¦ (Î»ğ“‹ "_" â†¦ var 1 []))))
_ = refl

const : {A B : Set} â†’ A â†’ B â†’ A
const a _ = a

_ : quoteTerm const â‰¡ def (quote const) []
_ = refl
#+END_SRC

Finally, here's an example of a section.
#+BEGIN_SRC org-agda
_ :   quoteTerm (_â‰¡ "b")
    â‰¡ Î»ğ“‹ "section" â†¦
       (def (quote _â‰¡_)
        (ğ’½ğ“‡ğ’¶ (def (quote Level.zero) []) âˆ·
         ğ’½ğ“‡ğ’¶(def (quote String) []) âˆ·
         ğ“‹ğ“‡ğ’¶ (var 0 []) âˆ·
         ğ“‹ğ“‡ğ’¶ (lit (string "b")) âˆ· []))
_ = refl
#+END_SRC

* Metaprogramming with The Typechecking Monad ~TC~
The ~TC~ monad provides an interface to Agda's type checker.
#+BEGIN_EXAMPLE org-agda
postulate
  TC       : âˆ€ {a} â†’ Set a â†’ Set a
  returnTC : âˆ€ {a} {A : Set a} â†’ A â†’ TC A
  bindTC   : âˆ€ {a b} {A : Set a} {B : Set b} â†’ TC A â†’ (A â†’ TC B) â†’ TC B
#+END_EXAMPLE

In order to use ~do~-notation we need to have the following definitions in scope.
#+BEGIN_SRC org-agda
_>>=_        : âˆ€ {a b} {A : Set a} {B : Set b} â†’ TC A â†’ (A â†’ TC B) â†’ TC B
_>>=_ = bindTC

_>>_        : âˆ€ {a b} {A : Set a} {B : Set b} â†’ TC A â†’ TC B â†’ TC B
_>>_  = Î» p q â†’ p >>= (Î» _ â†’ q)
#+END_SRC

The primitives of ~TC~ can be seen on the [[https://agda.readthedocs.io/en/v2.6.0/language/reflection.html#type-checking-computations][documentation]] page; below are a few notable
ones that we may use. Other primitives include support for the current context,
type errors, and metavariables.
#+BEGIN_EXAMPLE org-agda
postulate
  {- Take what you have and try to make it fit into the current goal. -}
  unify : (have : Term) (goal : Term) â†’ TC âŠ¤

  {- Try first computation, if it crashes with a type error, try the second. -}
  catchTC : âˆ€ {a} {A : Set a} â†’ TC A â†’ TC A â†’ TC A

  {- Infer the type of a given term. -}
  inferType : Term â†’ TC Type

  {- Check a term against a given type. This may resolve implicit arguments
     in the term, so a new refined term is returned. Can be used to create
     new metavariables: newMeta t = checkType unknown t -}
  checkType : Term â†’ Type â†’ TC Term

  {- Compute the normal form of a term. -}
  normalise : Term â†’ TC Term

  {- Quote a value, returning the corresponding Term. -}
  quoteTC : âˆ€ {a} {A : Set a} â†’ A â†’ TC Term

  {- Unquote a Term, returning the corresponding value. -}
  unquoteTC : âˆ€ {a} {A : Set a} â†’ Term â†’ TC A

  {- Create a fresh name. -}
  freshName : String â†’ TC Name

  {- Declare a new function of the given type. The function must be defined
     later using 'defineFun'. Takes an Arg Name to allow declaring instances
     and irrelevant functions. The Visibility of the Arg must not be hidden. -}
  declareDef : Arg Name â†’ Type â†’ TC âŠ¤

  {- Define a declared function. The function may have been declared using
     'declareDef' or with an explicit type signature in the program. -}
  defineFun : Name â†’ List Clause â†’ TC âŠ¤

  {- Get the type of a defined name. Replaces 'primNameType'. -}
  getType : Name â†’ TC Type

  {- Get the definition of a defined name. Replaces 'primNameDefinition'. -}
  getDefinition : Name â†’ TC Definition

  {-  Change the behaviour of inferType, checkType, quoteTC, getContext
      to normalise (or not) their results. The default behaviour is no
      normalisation. -}
  withNormalisation : âˆ€ {a} {A : Set a} â†’ Bool â†’ TC A â†’ TC A
#+END_EXAMPLE

~TC~ computations, or â€œmetaprogramsâ€, can be run by declaring them as macros or by
unquoting. Let's begin with the former.

* Unquoting â”€Making new functions & types

Recall our ~RGB~ example type was a simple enumeration consisting of ~Red, Green, Blue~.
Consider the singleton type:
#+BEGIN_EXAMPLE org-agda
data IsRed : RGB â†’ Set where
  yes : IsRed Red
#+END_EXAMPLE
The name ~Red~ completely determines this datatype; so let's try to generate it
mechanically. Unfortunately, as far as I could tell, there is currently no way
to unquote ~data~ declarations. As such, we'll settle for the following
isomorphic functional formulation:
#+BEGIN_EXAMPLE org-agda
IsRed : RGB â†’ Set
IsRed x = x â‰¡ Red
#+END_EXAMPLE

First, let's quote the relevant parts, for readability.
#+BEGIN_SRC org-agda
â€œâ„“â‚€â€ : Arg Term
â€œâ„“â‚€â€ = ğ’½ğ“‡ğ’¶ (def (quote Level.zero) [])

â€œRGBâ€ : Arg Term
â€œRGBâ€ = ğ’½ğ“‡ğ’¶ (def (quote RGB) [])

â€œRedâ€ : Arg Term
â€œRedâ€ = ğ“‹ğ“‡ğ’¶ (con (quote Red) [])
#+END_SRC
The first two have a nearly identical definition and it would be nice to
mechanically derive them...

Anyhow,
we use the ~unquoteDecl~ keyword, which allows us to obtain a ~NAME~ value, ~IsRed~.
We then quote the desired type, declare a function of that type, then define it
using the provided ~NAME~.
#+BEGIN_SRC org-agda
unquoteDecl IsRed =
  do ty â† quoteTC (RGB â†’ Set)
     declareDef (ğ“‹ğ“‡ğ’¶ IsRed) ty
     defineFun IsRed   [ clause [ ğ“‹ğ“‡ğ’¶ (var "x") ] (def (quote _â‰¡_) (â€œâ„“â‚€â€ âˆ· â€œRGBâ€ âˆ· â€œRedâ€ âˆ· ğ“‹ğ“‡ğ“‹ 0 [] âˆ· [])) ]
#+END_SRC
Let's try out our newly declared type.
#+BEGIN_SRC org-agda
red-is-a-solution : IsRed Red
red-is-a-solution = refl

green-is-not-a-solution : Â¬ (IsRed Green)
green-is-not-a-solution = Î» ()

red-is-the-only-solution : âˆ€ {c} â†’ IsRed c â†’ c â‰¡ Red
red-is-the-only-solution refl = refl
#+END_SRC

There is a major problem with using ~unquoteDef~ outright like this:
We cannot step-wise refine our program using holes ~?~, since that would
result in unsolved meta-variables. Instead, we split this process into two stages:
A programming stage, then an unquotation stage.

#+BEGIN_SRC org-agda
{- Definition stage, we can use â€˜?â€™ as we form this program. -}
define-Is : Name â†’ Name â†’ TC âŠ¤
define-Is is-name qcolour = defineFun is-name
  [ clause [ ğ“‹ğ“‡ğ’¶ (var "x") ] (def (quote _â‰¡_) (â€œâ„“â‚€â€ âˆ· â€œRGBâ€ âˆ· ğ“‹ğ“‡ğ’¶ (con qcolour []) âˆ· ğ“‹ğ“‡ğ“‹ 0 [] âˆ· [])) ]

declare-Is : Name â†’ Name â†’ TC âŠ¤
declare-Is is-name qcolour =
  do let Î· = is-name
     Ï„ â† quoteTC (RGB â†’ Set)
     declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
     defineFun is-name
       [ clause [ ğ“‹ğ“‡ğ’¶ (var "x") ]
         (def (quote _â‰¡_) (â€œâ„“â‚€â€ âˆ· â€œRGBâ€ âˆ· ğ“‹ğ“‡ğ’¶ (con qcolour []) âˆ· ğ“‹ğ“‡ğ“‹ 0 [] âˆ· [])) ]

{- Unquotation stage -}
IsRedâ€² : RGB â†’ Set
unquoteDef IsRedâ€² = define-Is IsRedâ€² (quote Red)

{- Trying it out -}
_ : IsRedâ€² Red
_ = refl
#+END_SRC

Notice that if we use â€œunquoteDefâ€, we must provide a type signature.
We only do so for illustration; the next code block avoids such a redundancy by
using â€œunquoteDeclâ€.

The above general approach lends itself nicely to the other data constructors as well:
#+BEGIN_SRC org-agda
unquoteDecl IsBlue  = declare-Is IsBlue  (quote Blue)
unquoteDecl IsGreen = declare-Is IsGreen (quote Green)

{- Example use -}
disjoint-rgb  : âˆ€{c} â†’ Â¬ (IsBlue c Ã— IsGreen c)
disjoint-rgb (refl , ())
#+END_SRC

The next natural step is to avoid manually invoking ~declare-Is~ for each constructor.
Unfortunately, it seems fresh names are not accessible, for some reason. ğŸ˜¢

For example, you would think the following would produce a function
named ~gentle-intro-to-reflection.identity~. Yet, it is not in scope.
I even tried extracting the definition to its own file and no luck.
#+BEGIN_SRC org-agda
unquoteDecl {- identity -}
  = do {- let Î· = identity -}
       Î· â† freshName "identity"
       Ï„ â† quoteTC (âˆ€ {A : Set} â†’ A â†’ A)
       declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
       defineFun Î· [ clause [ ğ“‹ğ“‡ğ’¶ (var "x") ] (var 0 []) ]

{- â€œidentityâ€ is not in scope!?
_ : âˆ€ {x : â„•}  â†’  identity x  â‰¡  x
_ = refl
-}
#+END_SRC

*Exercises*:
0. Comment out the ~freshName~ line above and uncomment the surrounding artifacts to so that the above
   unit test goes through.
1. Using that as a template, unquote a function ~everywhere-0 : â„• â†’ â„•~ that is constantly 0.
2. Unquote the constant combinator ~K : {A B : Set} â†’ A â†’ B â†’ A~.
#+BEGIN_EXAMPLE org-agda
unquoteDecl everywhere-0
  = do â‹¯

_ : everywhere-0 3 â‰¡ 0
_ = refl

unquoteDecl K
  = do â‹¯

_ : K 3 "cat" â‰¡ 3
_ = refl
#+END_EXAMPLE

*Bonus:* Proofs of a singleton type such as ~IsRed~ are essentially the same for all singelton types
over ~RGB~. Write, in two stages, a metaprogram that demonstrates each singleton type has a single member
â”€c.f., ~red-is-the-only-solution~ from above. Hint: This question is as easy as the ones before it.
#+BEGIN_EXAMPLE org-agda
{- Programming stage }
declare-unique : Name â†’ (RGB â†’ Set) â†’ RGB â†’ TC âŠ¤
declare-unique it S colour =
  = do â‹¯

{- Unquotation stage -}
unquoteDecl red-unique = declare-unique red-unique IsRed Red
unquoteDecl green-unique = declare-unique green-unique IsGreen Green
unquoteDecl blue-unique = declare-unique blue-unique IsBlue Blue

{- Test -}
_ : âˆ€ {c} â†’ IsGreen c â†’ c â‰¡ Green
_ = green-unique
#+END_EXAMPLE

:Solutions:
#+BEGIN_SRC org-agda
{- Exercise: -}
unquoteDecl everywhere-0
  = do let Î· = everywhere-0
       Ï„ â† quoteTC (â„• â†’ â„•)
       declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
       defineFun Î· [ clause [ ğ“‹ğ“‡ğ’¶ (var "x") ] (con (quote zero) []) ]

_ : everywhere-0 3 â‰¡ 0
_ = refl
{- End -}

{- Exercise: -}
unquoteDecl K
  = do let Î· = K
       Ï„ â† quoteTC ({A B : Set} â†’ A â†’ B â†’ A)
       declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
       defineFun Î· [ clause (ğ“‹ğ“‡ğ’¶ (var "x") âˆ· ğ“‹ğ“‡ğ’¶ (var "y") âˆ· []) (var 1 []) ]

_ : K 3 "cat" â‰¡ 3
_ = refl
{- End -}

{- Exercise: -}
declare-unique : Name â†’ (RGB â†’ Set) â†’ RGB â†’ TC âŠ¤
declare-unique it S colour =
  do let Î· = it
     Ï„ â† quoteTC (âˆ€ {c} â†’ S c â†’ c â‰¡ colour)
     declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
     defineFun Î· [ clause [ ğ“‹ğ“‡ğ’¶ (con (quote refl) []) ] (con (quote refl) []) ]

unquoteDecl red-unique = declare-unique red-unique IsRed Red
unquoteDecl green-unique = declare-unique green-unique IsGreen Green
unquoteDecl blue-unique = declare-unique blue-unique IsBlue Blue

_ : âˆ€ {c} â†’ IsGreen c â†’ c â‰¡ Green
_ = green-unique
{- End -}
#+END_SRC
:End:

:Failed_exploration:
#+BEGIN_EXAMPLE org-agda
RGB-constructors : Definition â†’ Name Ã— Name Ã— Name
RGB-constructors (data-type pars (x âˆ· y âˆ· z âˆ· cs)) = x , y , z
RGB-constructors _ = n , n , n where n = quote RGB

unquoteDecl
  =    do Î´ â† getDefinition (quote RGB)

          let r , g , b = RGB-constructors Î´
       -- TODO: get unqualified name, then prefix it with "Is",
       -- then make that into a new name. Then declare a function with that name.

          Î· â† freshName "IsX"
          -- let Î· = r
          Ï„ â† quoteTC (RGB â†’ Set)
          declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
          define-Is Î·

-- _ : {!!} -- IsX Red -- gentle-intro-to-reflection.IsX
-- _ = {!IsX!}
--
#+END_EXAMPLE
:End:

* Sidequest: Avoid tedious ~refl~ proofs

Time for a breather (â€¢Ì€á´—â€¢Ì)Ùˆ

Look around your code base for a function that makes explicit pattern matching, such as:
#+BEGIN_SRC org-agda
just-Red : RGB â†’ RGB
just-Red Red   = Red
just-Red Green = Red
just-Red Blue  = Red

only-Blue : RGB â†’ RGB
only-Blue Blue = Blue
only-Blue _   = Blue
#+END_SRC

Such functions have properties which cannot be proven unless we pattern match
on the arguments they pattern match. For example, that the above function is
constantly ~Red~ requires pattern matching then a ~refl~ for each clause.
#+BEGIN_SRC org-agda
just-Red-is-constant : âˆ€{c} â†’ just-Red c â‰¡ Red
just-Red-is-constant {Red}   = refl
just-Red-is-constant {Green} = refl
just-Red-is-constant {Blue}  = refl

{- Yuck, another tedious proof -}
only-Blue-is-constant : âˆ€{c} â†’ only-Blue c â‰¡ Blue
only-Blue-is-constant {Blue}  = refl
only-Blue-is-constant {Red}   = refl
only-Blue-is-constant {Green} = refl
#+END_SRC

In such cases, we can encode the general design decisions ---/pattern match and yield refl/---
then apply the schema to each use case.

Here's the schema:
#+BEGIN_SRC org-agda
constructors : Definition â†’ List Name
constructors (data-type pars cs) = cs
constructors _ = []

by-refls : Name â†’ Term â†’ TC âŠ¤
by-refls nom thm-you-hope-is-provable-by-refls
 = let mk-cls : Name â†’ Clause
       mk-cls qcolour = clause [ ğ’½ğ“‡ğ’¶ (con qcolour []) ] (con (quote refl) [])
   in
   do let Î· = nom
      Î´ â† getDefinition (quote RGB)
      let clauses = List.map mk-cls (constructors Î´)
      declareDef (ğ“‹ğ“‡ğ’¶ Î·) thm-you-hope-is-provable-by-refls
      defineFun Î· clauses
#+END_SRC

Here's a use case.
#+BEGIN_SRC org-agda
_ : âˆ€{c} â†’ just-Red c â‰¡ Red
_ = nice
  where unquoteDecl nice = by-refls nice (quoteTerm (âˆ€{c} â†’ just-Red c â‰¡ Red))
#+END_SRC

Note:
0. The first ~nice~ refers to the function
   created by the RHS of the unquote.

1. The RHS ~nice~ refers to the Name value provided
   by the LHS.

2. The LHS ~nice~ is a declaration of a Name value.

This is rather clunky since the theorem to be proven was repeated twice
â”€repetition is a signal that something's wrong! In the next section we
use macros to avoid such repetiton, as well as the ~quoteTerm~ keyword.

Note that we use a ~where~ clause since unquotation cannot occur in a ~let~,
for some reason.

Here's another use case of the proof pattern (â€¢Ì€á´—â€¢Ì)Ùˆ
#+BEGIN_SRC org-agda
_ : âˆ€{c} â†’ only-Blue c â‰¡ Blue
_ = nice
  where unquoteDecl nice = by-refls nice (quoteTerm âˆ€{c} â†’ only-Blue c â‰¡ Blue)
#+END_SRC

One proof pattern, multiple invocations!
Super neat stuff :grin:

* Macros â”€Abstracting Proof Patterns

 Macros are functions of type ~Ï„â‚€ â†’ Ï„â‚ â†’ â‹¯ â†’ Term â†’ TC âŠ¤~ that are defined in a
 ~macro~ block. The last argument is supplied by the type checker and denotes
 the â€œgoalâ€ of where the macro is placed: One generally unifies what they have
 with the goal, what is desired in the use site.

 Why the ~macro~ block?
 + Metaprograms can be run in a term position.
 + Without the macro block, we run computations using the ~unquote~ keyword.
 + Quotations are performed automatically; e.g.,
   if ~f : Term â†’ Name â†’ Bool â†’ Term â†’ TC âŠ¤~
   then an application ~f u v w~ desugars into
   ~unquote (f (quoteTerm u) (quote v) w)~.

   No syntactic overhead: Macros are applied like normal functions.

Macros cannot be recursive; instead one defines a recursive function outside the
macro block then has the macro call the recursive function.

** C-style macros

In the C language one defines a macro, say, by ~#define luckyNum 1972~ then later uses
it simply by the name ~luckyNum~. Without macros, we have syntactic overhead using
the ~unquote~ keyword:
#+BEGIN_SRC org-agda
luckyNumâ‚€ : Term â†’ TC âŠ¤
luckyNumâ‚€ h = unify h (quoteTerm 55)

numâ‚€ : â„•
numâ‚€ = unquote luckyNumâ‚€
#+END_SRC
Instead, we can achieve C-style behaviour by placing our metaprogramming code within a ~macro~ block.
#+BEGIN_SRC org-agda
macro
  luckyNum : Term â†’ TC âŠ¤
  luckyNum h = unify h (quoteTerm 55)

num : â„•
num = luckyNum
#+END_SRC
Unlike C, all code fragments must be well-defined.

*Exercise:* Write a macro to always yield the first argument in a function.
The second example shows how it can be used to access implicit arguments
without mentioning them :b
#+BEGIN_EXAMPLE org-agda
macro
  first : Term â†’ TC âŠ¤
  first goal = â‹¯

myconst : {A B : Set} â†’ A â†’ B â†’ A
myconst = Î» x â†’ Î» y â†’ first

mysum : ( {x} y : â„•) â†’ â„•
mysum y = y + first
#+END_EXAMPLE
:Solution:
#+BEGIN_SRC org-agda
{- exercise -}
macro
  first : Term â†’ TC âŠ¤
  first goal = unify goal (var 1 [])

myconst : {A B : Set} â†’ A â†’ B â†’ A
myconst = Î» x â†’ Î» y â†’ first

mysum : ( {x} y : â„•) â†’ â„•
mysum y = y + first
{- end -}
#+END_SRC
:End:

** Tedious Repetitive Proofs No More!
Suppose we wish to prove that addition, multiplication, and exponentiation
have right units 0, 1, and 1 respectively. We obtain the following nearly identical
proofs!

#+BEGIN_SRC org-agda
+-rid : âˆ€{n} â†’ n + 0 â‰¡ n
+-rid {zero}  = refl
+-rid {suc n} = cong suc +-rid

*-rid : âˆ€{n} â†’ n * 1 â‰¡ n
*-rid {zero}  = refl
*-rid {suc n} = cong suc *-rid

^-rid : âˆ€{n} â†’ n ^ 1 â‰¡ n
^-rid {zero}  = refl
^-rid {suc n} = cong suc ^-rid
#+END_SRC

There is clearly a pattern here screaming to be abstracted, let's comply â™¥â€¿â™¥

The natural course of action in a functional language is to try a higher-order combinator:
#+BEGIN_SRC org-agda
{- â€œfor loopsâ€ or â€œInduction for â„•â€ -}
foldn : (P : â„• â†’ Set) (base : P zero) (ind : âˆ€ n â†’ P n â†’ P (suc n))
      â†’ âˆ€(n : â„•) â†’ P n
foldn P base ind zero    = base
foldn P base ind (suc n) = ind n (foldn P base ind n)
#+END_SRC

Now the proofs are shorter:
#+BEGIN_SRC org-agda
_ : âˆ€ (x : â„•) â†’ x + 0 â‰¡ x
_ = foldn _ refl (Î» _ â†’ cong suc)    {- This and next two are the same -}

_ : âˆ€ (x : â„•) â†’ x * 1 â‰¡ x
_ = foldn _ refl (Î» _ â†’ cong suc)    {- Yup, same proof as previous -}

_ : âˆ€ (x : â„•) â†’ x ^ 1 â‰¡ x
_ = foldn _ refl (Î» _ â†’ cong suc)    {- No change, same proof as previous -}
#+END_SRC
Unfortunately, we are manually copy-pasting the same proof /pattern/.
#+begin_quote org
When you see repetition, copy-pasting, know that there is room for improvement! (â€¢Ì€á´—â€¢Ì)Ùˆ

Don't repeat yourself!
#+end_quote

Repetition can be mitigated a number of ways, including typeclasses or metaprogramming, for example.
The latter requires possibly less thought and it's the topic of this article, so let's do that :smile:

*Exercise*: Following the template of the previous exercises, fill in the missing parts below.
Hint: It's nearly the same level of difficulty as the previous exercises.
#+BEGIN_EXAMPLE org-agda
make-rid : (let A = â„•) (_âŠ•_ : A â†’ A â†’ A) (e : A) â†’ Name â†’ TC âŠ¤
make-rid _âŠ•_ e nom
 = do â‹¯

_ : âˆ€{x : â„•} â†’ x + 0 â‰¡ x
_ = nice where unquoteDecl nice = make-rid _+_ 0 nice
#+END_EXAMPLE
:Solution:
#+BEGIN_SRC org-agda
make-rid : (let A = â„•) (_âŠ•_ : A â†’ A â†’ A) (e : A) â†’ Name â†’ TC âŠ¤
make-rid _âŠ•_ e nom
 = do let Î· = nom
      let clauses =   clause [ ğ’½ğ“‡ğ’¶ (con (quote zero) []) ] (con (quote refl) [])
                    âˆ· clause [ ğ’½ğ“‡ğ’¶ (con (quote suc)  [ ğ“‹ğ“‡ğ’¶ (var "n") ]) ]
                             (def (quote cong) (ğ“‹ğ“‡ğ’¶ (quoteTerm suc) âˆ· ğ“‹ğ“‡ğ’¶ (def nom []) âˆ· [])) âˆ· []
      Ï„ â† quoteTC (âˆ€{x : â„•} â†’ x âŠ• e â‰¡ x)
      declareDef (ğ“‹ğ“‡ğ’¶ Î·) Ï„
      defineFun Î· clauses

_ : âˆ€{x : â„•} â†’ x + 0 â‰¡ x
_ = nice where unquoteDecl nice = make-rid _+_ 0 nice
#+END_SRC
:End:

There's too much syntactic overhead here, let's use macros instead.
#+BEGIN_SRC org-agda
macro
  _trivially-has-rid_ : (let A = â„•) (_âŠ•_ : A â†’ A â†’ A) (e : A) â†’ Term â†’ TC âŠ¤
  _trivially-has-rid_ _âŠ•_ e goal
   = do Ï„ â† quoteTC (Î»(x : â„•) â†’ x âŠ• e â‰¡ x)
        unify goal (def (quote foldn)            {- Using foldn    -}
          ( ğ“‹ğ“‡ğ’¶ Ï„                                {- Type P         -}
          âˆ· ğ“‹ğ“‡ğ’¶ (con (quote refl) [])            {- Base case      -}
          âˆ· ğ“‹ğ“‡ğ’¶ (Î»ğ“‹ "_" â†¦ quoteTerm (cong suc))  {- Inductive step -}
          âˆ· []))
#+END_SRC

Now the proofs have minimal repetition /and/ the proof pattern is written only /once/:
#+BEGIN_SRC org-agda
_ : âˆ€ (x : â„•) â†’ x + 0 â‰¡ x
_ = _+_ trivially-has-rid 0

_ : âˆ€ (x : â„•) â†’ x * 1 â‰¡ x
_ = _*_ trivially-has-rid 1

_ : âˆ€ (x : â„•) â†’ x * 1 â‰¡ x
_ = _^_ trivially-has-rid 1
#+END_SRC

Note we could look at the type of the goal, find the operator ~_âŠ•_~ and the unit;
they need not be passed in. Later we will see how to reach into the goal type
and pull pieces of it out for manipulation (â€¢Ì€á´—â€¢Ì)Ùˆ

It would have been ideal if we could have defined our macro without using ~foldn~;
I could not figure out how to do that. ğŸ˜§

Before one abstracts a pattern into a macro, it's useful to have a few instances
of the pattern beforehand. When abstracting, one may want to compare how we think
versus how Agda's thinking. For example, you may have noticed that in the previous
macro, Agda normalised the expression ~suc n + 0~ into ~suc (n + 0)~ by invoking the definition
of ~_+_~. We may inspect the goal of a function with the ~quoteGoal â‹¯ in â‹¯~ syntax:

#+BEGIN_SRC org-agda
+-ridâ€² : âˆ€{n} â†’ n + 0 â‰¡ n
+-ridâ€² {zero}  = refl
+-ridâ€² {suc n} = quoteGoal e in
  let
    suc-n : Term
    suc-n = con (quote suc) [ ğ“‹ğ“‡ğ’¶ (var 0 []) ]

    lhs : Term
    lhs = def (quote _+_) (ğ“‹ğ“‡ğ’¶ suc-n âˆ· ğ“‹ğ“‡ğ’¶ (lit (nat 0)) âˆ· [])

    {- Check our understanding of what the goal is â€œeâ€. -}
    _ : e â‰¡ def (quote _â‰¡_)
                 (ğ’½ğ“‡ğ’¶ (quoteTerm Level.zero) âˆ· ğ’½ğ“‡ğ’¶ (quoteTerm â„•)
                 âˆ· ğ“‹ğ“‡ğ’¶ lhs âˆ· ğ“‹ğ“‡ğ’¶ suc-n âˆ· [])
    _ = refl

    {- What does it look normalised. -}
    _ :   quoteTerm (suc (n + 0) â‰¡ n)
         â‰¡ unquote Î» goal â†’ (do g â† normalise goal; unify g goal)
    _ = refl
  in
  cong suc +-ridâ€²
#+END_SRC

It would be really nice to simply replace the last line by a macro, say ~induction~.
Unfortunately, for that I would need to obtain the name ~+-ridâ€²~, which as far as I could
tell is not possible with the current reflection mechanism.

* Our First Real Proof Tactic

When we have a proof ~p : x â‰¡ y~ it is a nuisance to have to write ~sym p~ to prove ~y â‰¡ x~
â”€we have to remember which â€˜directionâ€™ ~p~. Let's alleviate such a small burden, then use
the tools here to alleviate a larger burden later; namely, rewriting subexpressions.

Given ~p : x â‰¡ y~, we cannot simply yield ~def (quote sym) [ ğ“‹ğ“‡ğ’¶ p ]~ since ~sym~ actually
takes four arguments â”€compare when we quoted ~_â‰¡_~ earlier. Instead, we infer type of ~p~
to be, say, ~quoteTerm (_â‰¡_ {â„“} {A} x y)~. Then we can correctly provide all the required arguments.

#+BEGIN_SRC org-agda
â‰¡-type-info : Term â†’ TC (Arg Term Ã— Arg Term Ã— Term Ã— Term)
â‰¡-type-info (def (quote _â‰¡_) (ğ“ âˆ· ğ’¯ âˆ· arg _ l âˆ· arg _ r âˆ· [])) = returnTC (ğ“ , ğ’¯ , l , r)
â‰¡-type-info _ = typeError [ strErr "Term is not a â‰¡-type." ]
#+END_SRC

What if later we decided that we did not want a proof of ~x â‰¡ y~, but rather of ~x â‰¡ y~.
In this case, the orginal proof ~p~ suffices. Rather than rewriting our proof term, our
macro could try providing it if the symmetry application fails.

#+BEGIN_SRC org-agda
{- Syntactic sugar for trying a computation, if it fails then try the other one -}
try-fun : âˆ€ {a} {A : Set a} â†’ TC A â†’ TC A â†’ TC A
try-fun = catchTC

syntax try-fun t f = try t or-else f
#+END_SRC

With the setup in hand, we can now form our macro:
#+BEGIN_SRC org-agda
macro
  applyâ‚ : Term â†’ Term â†’ TC âŠ¤
  applyâ‚ p goal = try (do Ï„ â† inferType p
                          ğ“ , ğ’¯ , l , r â† â‰¡-type-info Ï„
                          unify goal (def (quote sym) (ğ“ âˆ· ğ’¯ âˆ· ğ’½ğ“‡ğ’¶ l âˆ· ğ’½ğ“‡ğ’¶ r âˆ· ğ“‹ğ“‡ğ’¶ p âˆ· [])))
                  or-else
                       unify goal p
#+END_SRC

For example,
#+BEGIN_SRC org-agda
postulate x y : â„•
postulate q : x + 2 â‰¡ y

{- Same proof yields two theorems! (à¸‡à² _à² )à¸‡ -}
_ : y â‰¡ x + 2
_ = applyâ‚ q

_ : x + 2 â‰¡ y
_ = applyâ‚ q
#+END_SRC

Let's furnish ourselves with the ability to inspect the /produced/ proofs.
#+BEGIN_SRC org-agda
{- Type annotation -}
syntax has A a = a âˆ¶ A -- â€œ\:â€

has : âˆ€ (A : Set) (a : A) â†’ A
has A a = a
#+END_SRC

Let's try this on an arbitrary type:
#+BEGIN_SRC org-agda
woah : {A : Set} (x y : A) â†’ x â‰¡ y â†’ (y â‰¡ x) Ã— (x â‰¡ y)
woah x y p = applyâ‚ p , applyâ‚ p

  where -- Each invocation generates a different proof, indeed:

  first-pf : (applyâ‚ p âˆ¶ (y â‰¡ x)) â‰¡ sym p
  first-pf = refl

  second-pf : (applyâ‚ p âˆ¶ (x â‰¡ y)) â‰¡ p
  second-pf = refl
#+END_SRC

*Exercise:* When we manually form a proof invoking symmetry we simply write, for example, ~sym p~
and the implict arguments are inferred. We can actually do the same thing here! We were a bit dishonest above. ğŸ‘‚
Rewrite ~applyâ‚~, call it ~applyâ‚‚, so that the ~try~ block is a single, unparenthesised, ~unify~ call.
:Solution:
#+BEGIN_SRC org-agda
macro
  applyâ‚‚ : Term â†’ Term â†’ TC âŠ¤
  applyâ‚‚ p goal = try unify goal (def (quote sym)  (ğ“‹ğ“‡ğ’¶ p âˆ· []))
                  or-else unify goal p

_ : {A : Set} (x y : A) â†’ x â‰¡ y â†’ (y â‰¡ x) Ã— (x â‰¡ y)
_ = Î» x y p â†’ applyâ‚‚ p , applyâ‚‚ p
#+END_SRC
:End:

*Exercise:* Extend the previous macro so that we can prove statements of the form ~x â‰¡ x~ regardless of what ~p~
proves. Aesthetics hint: ~try_or-else_~ doesn't need brackets in this case, at all.
#+BEGIN_EXAMPLE org-agda
macro
  applyâ‚ƒ : Term â†’ Term â†’ TC âŠ¤
  applyâ‚ƒ p goal = â‹¯

yummah : {A : Set} {x y : A} (p : x â‰¡ y)  â†’  x â‰¡ y  Ã—  y â‰¡ x  Ã—  y â‰¡ y
yummah p = applyâ‚ƒ p , applyâ‚ƒ p , applyâ‚ƒ p
#+END_EXAMPLE
:Solution:
#+BEGIN_SRC org-agda
macro
  applyâ‚ƒ : Term â†’ Term â†’ TC âŠ¤
  applyâ‚ƒ p goal = try unify goal (def (quote sym) (ğ“‹ğ“‡ğ’¶ p âˆ· []))
                  or-else try unify goal p
                          or-else unify goal (con (quote refl) [])

yummah : {A : Set} {x y : A} (p : x â‰¡ y)  â†’  x â‰¡ y  Ã—  y â‰¡ x  Ã—  y â‰¡ y
yummah p = applyâ‚ƒ p , applyâ‚ƒ p , applyâ‚ƒ p
#+END_SRC
:End:

*Exercise:* Write the following seemingly silly macro.
Hint: You cannot use the ~â‰¡-type-info~ method directly, instead you must invoke ~getType~ beforehand.
#+BEGIN_EXAMPLE org-agda
â‰¡-type-infoâ€² : Name â†’ TC (Arg Term Ã— Arg Term Ã— Term Ã— Term)
â‰¡-type-infoâ€² = â‹¯

macro
  sumSides : Name â†’ Term â†’ TC âŠ¤
  sumSides n goal = â‹¯

_ : sumSides q â‰¡ x + 2 + y
_ = refl
#+END_EXAMPLE
:Solution:
#+BEGIN_SRC org-agda
â‰¡-type-infoâ€² : Name â†’ TC (Arg Term Ã— Arg Term Ã— Term Ã— Term)
â‰¡-type-infoâ€² n = do Ï„ â† getType n; â‰¡-type-info Ï„

macro
  sumSides : Name â†’ Term â†’ TC âŠ¤
  sumSides n goal = do _ , _ , l , r â† â‰¡-type-infoâ€² n; unify goal (def (quote _+_) (ğ“‹ğ“‡ğ’¶ l âˆ· ğ“‹ğ“‡ğ’¶ r âˆ· []))

_ : sumSides q â‰¡ x + 2 + y
_ = refl
#+END_SRC
:End:

* TODO COMMENT ideas

+ macros left and right for â‰¡-type.

+ flatten: Take a nested record hierarchy and produce a flattened telescope, since
  records cannot be unquotes.

+ 2^50 * 3^313 â‰¡  3^313 * 2^50 is true by symmetry of *,
  but may timeout if we try to prove things by refl.

* COMMENT README

C-c C-c: evalute src block

#+NAME: make-readme
#+BEGIN_SRC emacs-lisp :results none
(with-temp-buffer
    (insert
    "#+EXPORT_FILE_NAME: README.md
     # HTML: <h1> gentle-intro-to-reflection </h1>

     A slow-paced introduction to reflection in Agda. ---Tactics!

     # The following can also be read as a [[https://alhassy.github.io/literate/][blog post]].

     # TOC: headlines 2
     #+INCLUDE: gentle-intro-to-reflection.lagda
    ")
    (org-mode)
    (org-md-export-to-markdown)
)
#+END_SRC

* COMMENT footer

The org-agda-mode and literate.el come from:
https://github.com/alhassy/org-agda-mode

# Having this with the local variables causes trees
# to remain folded when moving to agda2-mode.
#
(progn (org-babel-goto-named-src-block "make-readme") (org-babel-execute-src-block) (outline-hide-sublevels 1))

# Local Variables:
# eval: (visual-line-mode t)
# eval: (load-file "~/org-agda-mode/org-agda-mode.el")
# eval: (load-file "~/org-agda-mode/literate.el")
# End:
